load("@gmaven_rules//:defs.bzl", "gmaven_artifact")

licenses(["notice"])  # Apache 2.0

android_library(
    name = "IntentsBasicSampleLib",
    srcs = glob(["app/src/main/**/*.java"]),
    custom_package = "com.example.android.testing.espresso.BasicSample",
    manifest = "app/src/main/AndroidManifest.xml",
    resource_files = glob(["app/src/main/res/**/*"]),
    deps = [
        gmaven_artifact("com.android.support:support-annotations:27.0.2"),
        gmaven_artifact("com.android.support:support-compat:aar:27.0.2"),
        "@com_google_guava_guava//jar",
    ],
)

android_binary(
    name = "IntentsBasicSample",
    custom_package = "com.example.android.testing.espresso.BasicSample",
    manifest = "app/src/main/AppManifest.xml",
    deps = [":IntentsBasicSampleLib"],
)

android_library(
    name = "IntentsBasicSampleTestLib",
    srcs = glob(["app/src/androidTest/**/*.java"]),
    custom_package = "com.example.android.testing.espresso.BasicSample",
    deps = [
        ":IntentsBasicSampleLib",
        "//:test_deps",
        gmaven_artifact("com.android.support.test.espresso:espresso-intents:aar:3.0.1"),
    ],
)

android_binary(
    name = "IntentsBasicSampleTest",
    custom_package = "com.example.android.testing.espresso.BasicSample",
    instruments = ":IntentsBasicSample",
    manifest = "app/src/androidTest/AndroidManifest.xml",
    deps = [":IntentsBasicSampleTestLib"],
)

API_LEVELS = [
    "19_x86",
    "21_x86",
    "22_x86",
    "23_x86",
]

[android_instrumentation_test(
    name = "IntentsBasicSampleInstrumentationTest_%s" % API_LEVEL,
    target_device = "@android_test_support//tools/android/emulated_devices/generic_phone:android_%s_qemu2" % API_LEVEL,
    test_app = ":IntentsBasicSampleTest",
) for API_LEVEL in API_LEVELS]
